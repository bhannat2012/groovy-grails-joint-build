apply plugin: 'java'

defaultTasks 'jointBuild'

groovyCheckoutDir = '../GROOVY_2_0_X'
githubCheckoutDir = 'githubCheckout'
grailsCheckoutDir = "${githubCheckoutDir}/grails"

task buildGroovy(type:Exec) {
    workingDir groovyCheckoutDir

    def antHome = System.properties['ant.home']
    def antCommand = antHome ? "${antHome}/bin/ant" : 'ant'

    commandLine antCommand, 'clean', 'install', '-DskipTests=true', '-DskipExamples=true'
}

task testGrails(type: GradleBuild){
    groovyJarPath = new File(groovyCheckoutDir, 'target/dist/groovy-all-1.8.7-SNAPSHOT.jar')
    dir = grailsCheckoutDir
    startParameter.systemPropertiesArgs = ['groovy.jar': groovyJarPath.absolutePath, 'groovy.grails.joint': 'true']
    startParameter.excludedTaskNames = ['signArchives']
    tasks = ['clean', 'install', 'test']
}

task getGrails << {
    ant {
        mkdir dir: githubCheckoutDir
        get src:"http://nodeload.github.com/grails/grails-core/zipball/master", dest:"${githubCheckoutDir}/grails-core-src.zip", verbose:"true"
        unzip(src: "${githubCheckoutDir}/grails-core-src.zip", dest: githubCheckoutDir) {
            mapper type: 'regexp', from:/(grails-grails-core-[\w]*\/)(.*)/, to:/grails\/\2/
        }
    }
}

clean {
    delete githubCheckoutDir
}

task jointBuild(dependsOn: ['getGrails', 'buildGroovy', 'testGrails'])
